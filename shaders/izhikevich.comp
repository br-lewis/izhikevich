#version 460
layout(local_size_x = 1) in;

struct Neuron {
    // these are floats, not double. Not sure if double precision is necessary
    // but that's the default used by Matlab which is used in the example
    // implementation
    // but Metal doesn't support doubles at all

    // parameters
    float a;
    float b;
    float c;
    float d;

    // state
    // v is in mV
    float v;
    float u;
};

layout(set = 0, binding = 0) uniform Config {
    uint neuron_count;
    uint time_step;
};

layout(set = 0, binding = 1) buffer Input {
    float[] thalamic;
};

// possibly useful to split this buffer into 2
// 1 to hold the neuron parameters that don't change
// 1 to hold the neuron state which changes every tick
layout(set = 0, binding = 2) buffer Neurons {
    Neuron[] neurons;
};

layout(set = 0, binding = 3) buffer Spikes {
    uint[] spikes;
};

// possibly better as a texture?
layout(set = 0, binding = 4) buffer Connections {
    float[] connections;
};

uint flatten_index(uint width, uint x, uint y) {
    return (y * width) + x;
}

float connection_input(uint i, uint spike_i) {
    if (spike_i == 0) {
        return 0.0;
    }

    float total = 0.0;
    for (uint j=0; j < neuron_count; j++) {
        uint conn_index = flatten_index(neuron_count, i, j);
        uint spike_index = flatten_index(neuron_count, spike_i, j);
        total += connections[conn_index] * spikes[spike_index];
    }
    return total;
}

uint izhikevich_step(inout Neuron n, float i) {
    uint spike = 0;
    if (n.v >= 30.0) {
        n.v = n.c;
        n.u = n.u + n.d;
        spike = 1;
    }

    n.v = n.v + 0.5 * (0.04 * pow(n.v, 2) + 5.0 * n.v + 140.0 - n.u + i);
    n.v = n.v + 0.5 * (0.04 * pow(n.v, 2) + 5.0 * n.v + 140.0 - n.u + i);
    n.u = n.u + n.a * (n.b * n.v - n.u);

    return spike;
}

void main() {
    uint i = gl_GlobalInvocationID.x;

    float connection_input = connection_input(i, time_step);
    float thalamic_input = thalamic[i];

    uint spike_index = flatten_index(neuron_count, time_step, i);
    spikes[spike_index] = izhikevich_step(neurons[i], connection_input + thalamic_input);
}
