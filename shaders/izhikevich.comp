#version 460
layout(local_size_x = 1) in;

struct Neuron {
    // these are floats, not double. Not sure if double precision is necessary
    // but that's the default used by Matlab which is used in the example
    // implementation
    // but Metal doesn't support doubles at all

    // parameters
    float a;
    float b;
    float c;
    float d;

    // state
    // v is in mV
    float v;
    float u;
};

// possibly useful to split this buffer into 2
// 1 to hold the neuron parameters that don't change
// 1 to hold the neuron state which changes every tick
layout(set = 0, binding = 0) buffer Neurons {
    Neuron[] neurons;
};

layout(set = 0, binding = 1) buffer Spikes {
    uint[] spikes;
};

uint izhikevich_step(inout Neuron n) {
    uint spike = 0;
    if (n.v >= 30.0) {
        n.v = n.c;
        n.u = n.u + n.d;
        spike = 1;
    }

    // TODO: i is injected direct current, this will probably end up as another
    // buffer
    float i = -20.0;
    n.v = n.v + 0.5 * (0.04 * pow(n.v, 2) + 5.0 * n.v + 140.0 - n.u + i);
    n.v = n.v + 0.5 * (0.04 * pow(n.v, 2) + 5.0 * n.v + 140.0 - n.u + i);
    n.u = n.u + n.a * (n.b * n.v - n.u);

    return spike;
}

void main() {
    uint index = gl_GlobalInvocationID.x;
    spikes[index] = izhikevich_step(neurons[index]);
}
